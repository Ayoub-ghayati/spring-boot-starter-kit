buildscript {
    ext {
        springBootVersion = "${springBootVersion}"
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://repo.spring.io/libs-snapshot' }
        jcenter()
    }

    dependencies {
        classpath("org.jfrog.buildinfo:build-info-extractor-gradle:${jfrogBuildinfo}")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesVersion}")
        classpath("org.ajoberstar:grgit:${gradleGitVersion}")
    }
}

allprojects {
    apply from: "${rootDir}/gradle/versioning.gradle"
    apply from: "${rootDir}/gradle/git-properties.gradle"
    apply from: "${rootDir}/gradle/naming.gradle"

    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9

    apply plugin: "groovy"
    apply plugin: "jacoco"

    repositories {
        mavenCentral()
    }
}

configure(subprojects) {
    apply from: "${rootDir}/gradle/test.gradle"
}

configure(subprojects.findAll { it.name == 'sbsk-integration-test' }) {
    apply from: "${rootDir}/gradle/integration-test.gradle"
}

bootJar {
    manifest {
        attributes 'Start-Class': 'com.sbsk.web.WebApplication'
    }
}

dependencies {
    compile project(":sbsk-web")

    runtime("org.springframework.boot:spring-boot-properties-migrator")
}

apply from: "${rootDir}/gradle/jacoco-multi-project.gradle"
